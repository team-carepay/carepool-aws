plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
    id 'checkstyle'
    id 'org.sonarqube' version '2.7.1'
    id 'net.researchgate.release' version '2.8.1'
    id 'net.ltgt.errorprone' version '1.1.1'
}

group = 'com.carepay'

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.current()
}

repositories {
    mavenCentral()
    maven {
        url 's3://cp-maven-repo/releases'
        authentication {
            awsIm(AwsImAuthentication)
        }
    }
}

dependencies {
    implementation 'com.github.cliftonlabs:json-simple:3.1.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.13.2'
    testImplementation 'org.mockito:mockito-core:2.28.2'

    errorprone 'com.google.errorprone:error_prone_core:2.3.4'
    errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url 's3://cp-maven-repo/releases'
            authentication {
                awsIm(AwsImAuthentication)
            }
        }
    }
}

checkstyle {
    config = project.resources.text.fromUri('https://carepaydev.bitbucket.io/checkstyle-1.1.xml')
}

tasks.sonarqube.dependsOn tasks.jacocoTestReport
jacocoTestReport {
    reports {
        xml.enabled true
    }
}

release {
    tagCommitMessage = 'Release tag [skip ci]'
    newVersionCommitMessage = 'New version [skip ci]'
    git {
        requireBranch = ''
    }
}

afterReleaseBuild.dependsOn publish
